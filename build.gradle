
buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.hurricup:gradle-grammar-kit-plugin:2017.1.1"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.14'
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
}

group 'com.artuslang'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/jetbrains/markdown' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'

sourceSets {
    main {
        java {
            srcDir 'gen/java'
        }
    }
}

intellij {
    version 'IC-2017.1.5'
    updateSinceUntilBuild false
}

apply plugin: 'org.jetbrains.grammarkit'

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateLexer(type: GenerateLexer) {
    source = "src/grammar/artuslang.flex"
    targetDir = "gen/java/com/artuslang/lang/lexer/"
    targetClass = "ArtusLexer"
    purgeOldFiles = true
}

task generateParser(type: GenerateParser) {
    source = "src/grammar/artuslang.bnf"
    targetRoot = 'gen/java/'
    pathToParser = 'com/artuslang/lang/parser/ArtusParser.java'
    pathToPsiRoot = 'com/artuslang/lang/psi'
    purgeOldFiles = true
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn generateLexer
    dependsOn generateParser
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn generateLexer
    dependsOn generateParser
}
